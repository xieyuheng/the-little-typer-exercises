---
title: 6. Precisely How Many?
date: 2021-05-06
---

# dependency

```cicada
import { Nat, nat_rec, zero, one, two, three, four } from "./02.md"
import { List } from "./05.md"
```

## zero_add1_equal_absurd

```cicada
function nat_equal_consequence_t(x: Nat, y: Nat): Type {
  return nat_rec(
    x,
    nat_rec(
      y,
      Trivial,
      (_, _) => Absurd),
    (prev_x, _) =>
      nat_rec(
        y,
        Absurd,
        (prev_y, _) => Equal(Nat, prev_x, prev_y)))
}

function nat_equal_consequence_same(x: Nat): nat_equal_consequence_t(x, x) {
  return induction (x) {
    motive (target) => nat_equal_consequence_t(target, target)
    case zero => sole
    case add1(_prev, _almost) => refl
  }
}

function use_nat_equal(
  x: Nat, y: Nat,
  nat_equal: Equal(Nat, x, y),
): nat_equal_consequence_t(x, y) {
  function motive(n: Nat): Type {
    return nat_equal_consequence_t(x, n)
  }

  let base: motive(x) = nat_equal_consequence_same(x)

  return replace(nat_equal, motive, base)
}

function zero_add1_equal_absurd(n: Nat): (Equal(Nat, Nat.zero, Nat.add1(n))) -> Absurd {
  return use_nat_equal(zero, Nat.add1(n))
}
```

## sub1_equal

```cicada
function sub1_equal(
  x: Nat, y: Nat,
): (Equal(Nat, Nat.add1(x), Nat.add1(y))) -> Equal(Nat, x, y) {
  return use_nat_equal(Nat.add1(x), Nat.add1(y))
}
```

# Vector

```cicada
datatype Vector(E: Type) (length: Nat) {
  null: Vector(E, zero)
  cons(
    vague prev: Nat,
    head: E,
    tail: Vector(E, prev),
  ): Vector(E, Nat.add1(prev))
}
```

# example vectors

```cicada
compute the(Type, Vector(String, zero))
compute the(Type, Vector(String, one))
compute the(Type, Vector(List(String), zero))
compute the(Type, Vector(List(String), one))

compute the(Vector(String, zero), Vector.null)
compute the(Vector(List(String), zero), Vector.null)
compute the(Vector(List(List(String)), zero), Vector.null)

compute the(Vector(String, one), Vector.cons("a", Vector.null))
compute the(Vector(String, two), Vector.cons("a", Vector.cons("b", Vector.null)))

let a: Vector(String, one) = Vector.cons("a", Vector.null)
let ab: Vector(String, two) = Vector.cons("a", Vector.cons("b", Vector.null))
let abc: Vector(String, three) = Vector.cons("a", Vector.cons("b", Vector.cons("c", Vector.null)))
let abcd: Vector(String, four) = Vector.cons("a", Vector.cons("b", Vector.cons("c", Vector.cons("d", Vector.null))))
```

# vector_head

```cicada
function vector_head(
  implicit E: Type,
  implicit n: Nat,
  vector: Vector(E, Nat.add1(n))
): E {
  function motive(length: Nat, target: Vector(E, length)): Type {
    return (j: Nat, equal: Equal(Nat, length, Nat.add1(j))) -> E
  }

  let aux: motive(Nat.add1(n), vector) = {
    let base: motive(zero, Vector.null) =
      (j, equal) => from_falsehood_anything(zero_add1_equal_absurd(j, equal), E)

    function step(
      prev: Nat,
      head: E,
      tail: Vector(E, prev),
      almost: motive(prev, tail)
    ): motive(Nat.add1(prev), Vector.cons(head, tail)) {
      return (j, equal) => head
    }

    return induction (vector) {
      motive motive
      case null => base
      case cons(vague prev, head, tail, almost) =>
        step(prev, head, tail, almost.tail)
    }
  }

  return aux(n, refl)
}

compute vector_head(a)
compute vector_head(ab)
compute vector_head(abc)
compute vector_head(abcd)
```

# vector_tail

```cicada
function vector_tail(
  implicit E: Type,
  implicit n: Nat,
  vector: Vector(E, Nat.add1(n))
): Vector(E, n) {
  function motive(length: Nat, target: Vector(E, length)): Type {
    return (j: Nat, equal: Equal(Nat, length, Nat.add1(j))) -> Vector(E, j)
  }

  let aux: motive(Nat.add1(n), vector) = {
    let base: motive(zero, Vector.null) =
      (j, equal) => from_falsehood_anything(zero_add1_equal_absurd(j, equal), Vector(E, j))

    function step(
      prev: Nat,
      head: E,
      tail: Vector(E, prev),
      almost: (j: Nat, equal: Equal(Nat, prev, Nat.add1(j))) -> Vector(E, j)
    ): (j: Nat, equal: Equal(Nat, Nat.add1(prev), Nat.add1(j))) -> Vector(E, j) {
      return (j, equal) => replace(sub1_equal(prev, j, equal), (x) => Vector(E, x) , tail)
    }

    return induction (vector) {
      motive motive
      case null => base
      case cons(vague prev, head, tail, almost) =>
        step(prev, head, tail, almost.tail)
    }
  }

  return aux(n, refl)
}

compute vector_tail(a)
compute vector_tail(ab)
compute vector_tail(abc)
compute vector_tail(abcd)
```

# type check or infer error

`infer` error is described as "exp is not described by a type", for example:

```
What is the value of
  (first-of-one Atom vecnil)?

That question is meaningless because
  (first-of-one Atom vecnil)
is not described by a type, and this is
because
  vecnil
is not a
  (Vec Atom 1).
```

# Use a More Specific Type

Make a function total by using a more specific type to
rule out unwanted arguments.
