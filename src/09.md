---
title: 9. Double Your Money, Get Twice as Much
date: 2021-05-26
---

# Exercises

``` cicada
import { cong } from "./08.md"
import { vector_head, vector_tail } from "./06.md"

function add(x: Nat, y: Nat): Nat {
  return nat_rec(x, y, (_prev, almost) => add1(almost))
}

// NOTE increase
function incr(x: Nat): Nat {
  return nat_rec(x, 1, (_prev, almost) => add(1, almost))
}

the(Equal(Nat, incr(3), add1(3)), refl)

function incr_add1_equal_motive(n: Nat): Type {
  return Equal(Nat, incr(n), add1(n))
}

let incr_add1_equal_base: Equal(Nat, incr(zero), add1(zero)) = refl

let incr_add1_equal_step_t: Type = (
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
) -> Equal(Nat, add1(incr(prev)), add1(add1(prev)))

// NOTE Let's prove `incr_add1_equal_step` use `replace` directly.
function incr_add1_equal_step(
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
): Equal(Nat, add1(incr(prev)), add1(add1(prev))) {
  function motive(n: Nat): Type {
    return Equal(Nat, add1(incr(prev)), add1(n))
  }

  let base: Equal(Nat, add1(incr(prev)), add1(incr(prev))) = refl

  return replace(almost, motive, base)
}

function incr_add1_equal(n: Nat): Equal(Nat, incr(n), add1(n)) {
  return nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step)
}

incr_add1_equal(0)
incr_add1_equal(1)
incr_add1_equal(2)
incr_add1_equal(3)

function double(n: Nat): Nat {
  return nat_rec(n, 0, (_prev, almost) => add1(add1(almost)))
}

double(1)
double(2)
double(3)
double(4)

function twice(n: Nat): Nat {
  return add(n, n)
}

twice(1)
twice(2)
twice(3)
twice(4)

function add1_add_commute_on_2nd_arg(n: Nat, j: Nat): Equal(Nat, add1(add(n, j)), add(n, add1(j))) {
  function motive(j: Nat, target: Nat): Type {
    return Equal(Nat, add1(add(target, j)), add(target, add1(j)))
  }

  let base: Equal(Nat, add1(add(0, j)), add(0, add1(j))) = refl

  function step(
    prev: Nat,
    almost: Equal(Nat, add1(add(prev, j)), add(prev, add1(j)))
  ): Equal(Nat, add1(add1(add(prev, j))), add1(add(prev, add1(j)))) {
    return cong(almost, add(1))
  }

  return nat_ind(n, motive(j), base, step)
}

function twice_double_equal_step(
  prev: Nat,
  almost: Equal(Nat, add(prev, prev), double(prev))
): Equal(Nat, add1(add(prev, add1(prev))), add1(add1(double(prev)))) {
  let target: Equal(Nat, add1(add(prev, prev)), add(prev, add1(prev))) =
    add1_add_commute_on_2nd_arg(prev, prev)

  function motive(target: Nat): Type {
    return Equal(Nat, add1(target), add1(add1(double(prev))))
  }

  let base: Equal(Nat, add1(add1(add(prev, prev))), add1(add1(double(prev)))) =
    cong(almost, add(2))

  return replace(target, motive, base)
}

function twice_double_equal(n: Nat): Equal(Nat, twice(n), double(n)) {
  function motive(target: Nat): Type {
    return Equal(Nat, twice(target), double(target))
  }

  let base: Equal(Nat, 0, 0) = refl

  return nat_ind(n, motive, base, twice_double_equal_step)
}

twice_double_equal(17)

let twice_double_equal_on_17: Equal(Nat, twice(17), double(17)) = twice_double_equal(17)
let twice_double_equal_on_17_again: Equal(Nat, twice(17), double(17)) = refl

function vector_double(
  E: Type,
  length: Nat,
): (Vector(E, length)) -> Vector(E, double(length)) {
  function motive(target: Nat): Type {
     return (Vector(E, target)) -> Vector(E, double(target))
  }

  let base: (Vector(E, 0)) -> Vector(E, double(0)) = (vector) => vecnil

  function step(prev: Nat, doubler: motive(prev)): motive(add1(prev)) {
    return (vector) => {
      let head = vector_head(vector)
      return vec(head, vec(head, doubler(vector_tail(vector))))
    }
  }

  return nat_ind(length, motive, base, step)
}

vector_double(String, 3, vec! ["a", "b", "c"])

function symm(T: Type, from:T, to: T, target: Equal(T, from, to)): Equal(T, to, from) {
  return replace(target, (x) => Equal(T, x, from) , refl)
}

function vector_twice(
  E: Type,
  length: Nat,
  vector: Vector(E, length)
): Vector(E, twice(length)) {
  let target: Equal(Nat, double(length), twice(length)) =
    symm(Nat, twice(length), double(length), twice_double_equal(length))

  function motive(length: Nat): Type {
    return Vector(E, length)
  }

  let base = vector_double(E, length, vector)

  return replace(target, motive, base)
}

vector_twice(String, 3, vec! ["a", "b", "c"])
```

# Note about `replace`

But if `replace` replaces the `from` with
the `to`, why should we abstract over the `to`,
rather than the `from`?

Because first we know `motive(to)`,
thus use abstract `to` to get `motive`,
then we try to find exp of type `motive(from)`,
to apply `replace` to get `motive(to)`.
