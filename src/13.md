---
title: 13. Even Haf a Baker's Dozen
---

# dependency

```cicada
import { Nat, nat_rec, nat_ind } from "./02.md"
import { zero, one, two, three, four, five, six, seven, eight, nine, ten } from "./02.md"
import { List } from "./05.md"
```

# Either

```cicada
datatype Either(L: Type, R: Type) {
  inl(left: L): Either(L, R)
  inr(right: R): Either(L, R)
}
```

# Use `Either` to express "or" as a type

```cicada
compute the(Type, Either(Nat, String))
compute the(Either(Nat, String), Either.inl(six))
compute the(Either(Nat, String), Either.inr("abc"))
```

Let's use define a useless `size` to test induction of `Either`.

```cicada
import { length } from "./05.md"

function size(either: Either(Nat, List(String))): Nat {
  return recursion (either) {
    case inl(n) => n
    case inr(list) => length(String, list)
  }
}

compute size(Either.inl(three))
compute size(Either.inr(List.cons("a", List.cons("b", List.cons("c", List.null)))))
```

# `even_or_odd`

Every natural number is even or odd.

```cicada
import {
  Even,
  Odd,
  zero_is_even,
  // one_is_odd,
  even_add1_is_odd,
  odd_add1_is_even,
} from "./12.md"


function even_or_odd(n: Nat): Either(Even(n), Odd(n)) {
  function motive(target: Nat): Type {
    return Either(Even(target), Odd(target))
  }

  let base: Either(Even(zero), Odd(zero)) = Either.inl(zero_is_even)

  function step(
    prev: Nat,
    almost: Either(Even(prev), Odd(prev))
  ): Either(Even(Nat.add1(prev)), Odd(Nat.add1(prev))) {
    return recursion (almost) {
      case inl(even) => Either.inr(even_add1_is_odd(prev, even))
      case inr(odd) => Either.inl(odd_add1_is_even(prev, odd))
    }
  }

  return nat_ind(n, motive, base, step)
}
```

Use "same-as" chart to find out the following values.

At each step, look for the parts of expressions that change and those that don't.
Try to identify motives, bases, and steps that appear multiple times.

```cicada
compute even_or_odd(zero)
compute even_or_odd(one)
compute even_or_odd(two)
compute even_or_odd(three)
compute even_or_odd(four)
compute even_or_odd(five)
compute even_or_odd(six)
compute even_or_odd(seven)
```
