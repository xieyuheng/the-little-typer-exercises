---
title: 13. Even Haf a Baker's Dozen
---

# dependency

``` cicada
import { List } from "./05.md"
```

# Use `Either` to express "or" as a type

``` cicada
the(Type, Either(Nat, String))
the(Either(Nat, String), inl(6))
the(Either(Nat, String), inr("abc"))
```

Let's use define a useless `size` to test `either_ind`.

``` cicada
import { length } from "./05.md"

function size(either: Either(Nat, List(String))): Nat {
  return either_ind(
    either,
    (_) => Nat,
    (n) => n,
    (list) => length(String, list))
}

size(inl(3))
size(inr(List.cons("a", List.cons("b", List.cons("c", List.null)))))
```

# `even_or_odd`

Every natural number is even or odd.

``` cicada
import {
  Even,
  Odd,
  zero_is_even,
  // one_is_odd,
  even_add1_is_odd,
  odd_add1_is_even,
} from "./12.md"


function even_or_odd(n: Nat): Either(Even(n), Odd(n)) {
  function motive(target: Nat): Type {
    return Either(Even(target), Odd(target))
  }

  let base: Either(Even(0), Odd(0)) = inl(zero_is_even)

  function step(
    prev: Nat,
    almost: Either(Even(prev), Odd(prev))
  ): Either(Even(add1(prev)), Odd(add1(prev))) {
    return either_ind(
      almost,
      (_) => Either(Even(add1(prev)), Odd(add1(prev))),
      (even) => inr(even_add1_is_odd(prev, even)),
      (odd) => inl(odd_add1_is_even(prev, odd))
    )
  }

  return nat_ind(n, motive, base, step)
}
```

Use "same-as" chart to find out the following values.

At each step, look for the parts of expressions that change and those that don't.
Try to identify motives, bases, and steps that appear multiple times.

``` cicada
even_or_odd(0)
even_or_odd(1)
even_or_odd(2)
even_or_odd(3)
even_or_odd(4)
even_or_odd(5)
even_or_odd(6)
even_or_odd(7)
```
